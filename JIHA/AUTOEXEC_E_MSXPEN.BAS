10 'JIHA JONG ORIGINAL IS 1989, MODIFY IN 2023
20 KEYOFF:DEFINTA-Z:DEFUSR=&H156:COLOR15,1,1:X=1
30 GOSUB9000
40 C$=CHR$(&H1D)+CHR$(&H1D)+CHR$(&H1F)
50 A$(0)="`a"+C$+"bc"+CHR$(&H1E)
60 A$(1)="de"+C$+"fg"+CHR$(&H1E)
70 A$(2)="hi"+C$+"jk"+CHR$(&H1E)
80 A$(3)="lm"+C$+"no"+CHR$(&H1E)
90 A$(4)="pq"+C$+"rs"+CHR$(&H1E)
100 A$(5)="xy"+C$+"z{"+CHR$(&H1E)
110 A$(6)="♠♥"+C$+"♣♦"+CHR$(&H1E)
111 A$(7)="ぃぅ"+C$+"ぇぉ"+CHR$(&H1E)
112 COLOR=(2,0,3,0)
113 COLOR=(6,4,0,0)
114 COLOR=(10,7,6,0)
115 PRINT:PRINT" JIHA JONG 1989,2023"
116 PRINT:INPUT" DIFICULTY(1-51)";L
117 IFL<=0ORL>51THEN116
120 DIMD(27):CLS:OO=1:OY=1:MS=500-((L-1)*10):ES=500
130 FORI=0TO27:D(I)=-1:NEXT
140 A=RND(-TIME):RESTORE200
150 FORI=0TO27
151 J=RND(1)*28
160 IFD(J)=-1THENREADD(J)ELSE151
170 LOCATE9+X,10:PRINT"┌──────────┐"
180 LOCATE9+X,11:PRINTUSING"│*WAIT**##*│";28-I:LOCATE9+X,12:PRINT"└──────────┘"
190 NEXTI
200 DATA 0,0,0,0,1,1,1,1,2,2,2,2,3,3,3,3,4,4,4,4,5,5,5,5,6,6,6,6
210 CLS
220 E(0)=D(0)
230 E(1)=D(1)
240 M(0)=D(2)
250 M(1)=D(3)
260 E(2)=D(4)
270 E(3)=D(5)
280 M(2)=D(6):E=0:FORI=0TO3:IFE(I)<4THENE=E+1:NEXT:ELSENEXT
290 M(3)=D(7):TU=8:MX=0:MY=13:R=0:R1=0:RR=0:EX=0:EY=9:P=0:IFE>=3THENE=0ELSEE=1
300 LOCATE5+X,2:PRINTA$(7)+A$(7)+A$(7)+A$(7)
310 GOSUB500:LOCATE5+X,19:PRINTA$(M(0))+A$(M(1))+A$(M(2))+A$(M(3))
320 LOCATE16+X,5:PRINT"┌───────────┐"
321 LOCATE16+X,6:PRINT"│*JIHA*JONG*│"
322 LOCATE16+X,7:PRINT"├───────────┤"
323 LOCATE16+X,8:PRINT"│***********│"
324 LOCATE16+X,9:PRINT"│***********│"
325 LOCATE18+X,8:PRINT"DORA*";A$(D(26))
326 LOCATE16+X,10:PRINT"├───────────┤"
327 LOCATE16+X,11:PRINTUSING"│YOU**###000│";MS
328 LOCATE16+X,12:PRINTUSING"│COM**###000│";ES
329 LOCATE16+X,13:PRINT"│*LEFT***17*│":LOCATE16+X,14:PRINT"└───────────┘"
330 '
340 ONOYGOSUB1000,2000
350 OY=OY+1:IFOY>2THENOY=1
360 IFOY=2ANDR>0THENR=R+1
361 IFOY=1ANDR1>0THENR1=R1+1
370 GOTO 340
500 'SHUFFLE
501 K=0:J=0
510 FORI=0TO3:A(I)=M(I):NEXT
520 FORI=0TO3:IFA(I)=KTHENM(J)=A(I):J=J+1
530 IFJ>3THENRETURN
540 NEXTI
550 K=K+1:GOTO520
600 'DEFEAT SHUFFLE
601 K=0:J=0
610 FORI=0TO4:A(I)=M(I):NEXT
620 FORI=0TO4:IFA(I)=KTHENM(J)=A(I):J=J+1
630 IFJ>4THENRETURN
640 NEXTI
650 K=K+1:GOTO620
1000 'PLAYER COMMAND
1001 FORI=0TO4
1010 LOCATE1+X,22:PRINT"1DRAW  2CALL  3RON"
1020 A=USR(0)
1030 A$=INPUT$(1):A=VAL(A$):BEEP
1040 ONAGOTO1060,1300
1050 IFA=3THENGOSUB1400:GOTO 4000ELSE1010
1060 'DRAW
1070 M(4)=D(TU):TU=TU+1:GOSUB2600
1080 LOCATE14+X,19:PRINTA$(M(4))
1090 LOCATE1+X,22:PRINTSTRING$(30," ");
1100 LOCATE0+X,22:PRINT" RID 1 2 3 4  5 6REACH 7FINISH"
1110 A=USR(0):A$=INPUT$(1)
1120 BEEP:A=VAL(A$)
1130 IFA=0THEN1110
1140 IFA=7THENT=0:GOSUB1420:GOTO4000
1141 IFA=5THENLOCATE14+X,19:PRINT"  "+C$+"  ":GOTO1200
1142 IFA=6THEN1143ELSE1150
1143 IFR=0ANDP=0THENR=1:RR=0:BEEP:GOTO1100ELSEIFP>0ORR>0THENGOTO1100
1150 IFR<2ANDP<2THENSWAP M(A-1),M(4):LOCATE5+X+((A-1)*2),19:PRINT"  "+C$+"  ":GOTO1200ELSE1100
1200 IFR=1ANDMY=13THENLOCATEMX+X,12:PRINT"REACH"ELSEIFR=1ANDMY=15THENLOCATEMX+X,17:PRINT"REACH"
1201 LOCATEMX+X,MY:PRINTA$(M(4)):MX=MX+2:IFMX>12THENMX=0:MY=MY+2
1202 IFP=1THENP=2
1210 GOSUB500
1220 LOCATE5+X,19:PRINTA$(M(0))+A$(M(1))+A$(M(2))+A$(M(3)):LOCATE14+X,19:PRINT"  "+C$+"  "
1230 LOCATE0+X,22:PRINTSTRING$(30," ");:IFTU>24THEN3000ELSERETURN
1300 'PONG(CALL)
1301 IFR>0THENGOTO1000
1310 P=1:M(4)=E(4)
1320 GOSUB600:LOCATE5+X,19:PRINTA$(M(0))+A$(M(1))+A$(M(2))+A$(M(3)):LOCATE14+X,19:PRINTA$(M(4))
1330 BEEP:BEEP:LOCATE5+X,18:PRINT"CALLED"
1340 GOTO1090
1400 'RON(MAHJONG)
1410 T=1:M(4)=E(4)
1420 A$=""
1430 GOSUB600:FORI=0TO4:A$=A$+STR$(M(I)):NEXT
1440 IFLEFT$(A$,8)<>" 0 1 2 3"THEN1470
1450 IFVAL(RIGHT$(A$,1))<3THENY$="SINGLE*FOUR*WINDS":Y=2:RETURN
1460 IFVAL(RIGHT$(A$,1))>3THENY$="SINGLE*FOUR*WINDS":Y=1:RETURN
1470 IFA$=" 0 0 1 2 3"ORA$=" 0 1 1 2 3"ORA$=" 0 1 2 2 3"ORA$=" 0 1 2 3 3"THENY$="PAIR*FOUR*WINDS":Y=2:RETURN
1480 IFRIGHT$(A$,6)<>" 4 5 6"THEN1500
1490 B$=LEFT$(A$,4):IFB$=" 0 0"ORB$=" 1 1"ORB$=" 2 2"ORB$=" 3 3"THENY$="SINGLE*DRAGONS":Y=1:RETURN
1500 IFA$=" 4 4 5 5 6"ORA$=" 4 5 5 6 6"ORA$=" 4 4 5 6 6"THENY$="PAIR*DRAGONS":Y=2:RETURN
1510 B$=LEFT$(A$,6):IFB$=" 0 0 0"ORB$=" 1 1 1"ORB$=" 2 2 2"ORB$=" 3 3 3"ORB$=" 4 4 4"ORB$=" 5 5 5"ORB$=" 6 6 6"THEN1520ELSE1521
1520 B$=RIGHT$(A$,4):IFB$=" 0 0"ORB$=" 1 1"ORB$=" 2 2"ORB$=" 3 3"ORB$=" 4 4"ORB$=" 5 5"ORB$=" 6 6"THENY$="ONE*TRIPLES":Y=1:RETURN
1521 B$=RIGHT$(A$,6):IFB$=" 0 0 0"ORB$=" 1 1 1"ORB$=" 2 2 2"ORB$=" 3 3 3"ORB$=" 4 4 4"ORB$=" 5 5 5"ORB$=" 6 6 6"THEN1522ELSE1530
1522 B$=LEFT$(A$,4):IFB$=" 0 0"ORB$=" 1 1"ORB$=" 2 2"ORB$=" 3 3"ORB$=" 4 4"ORB$=" 5 5"ORB$=" 6 6"THENY$="ONE*TRIPLES":Y=1:RETURN
1530 B$=LEFT$(A$,8):IFB$=" 0 0 1 1"ORB$=" 0 0 2 2"ORB$=" 0 0 3 3"ORB$=" 2 2 3 3"THEN1540ELSE1541
1540 B$=RIGHT$(A$,1):IFVAL(B$)<4THENY$="PAIR*WINDS":Y=1:RETURN
1541 IFA$=" 0 0 1 3 3"ORA$=" 0 0 2 3 3"ORA$=" 0 0 1 2 2"THENY$="PAIR*WINDS":Y=1:RETURN
1550 IFB$=" 1 1 2 2"ANDVAL(RIGHT$(A$,1))<4THENY$="PAIR*WINDS,SW*WAR":Y=3:RETURN
1560 IFB$=" 1 1 2 2"ANDVAL(RIGHT$(A$,1))>3THENY$="SOUTH-WEST*WAR":Y=1:RETURN
1570 IFB$=" 1 1 3 3"ANDVAL(RIGHT$(A$,1))<4THENY$="PAIR*WINDS,CIVIL*WAR":Y=3:RETURN
1580 IFB$=" 1 1 3 3"ANDVAL(RIGHT$(A$,1))>3THENY$="CIVIL*WAR":Y=1:RETURN
1590 IFA$=" 1 1 2 3 3"ORA$=" 0 1 1 3 3"THENY$="PAIR*WINDS,SW*WAR":Y=3:RETURN
1600 IFA$=" 0 1 1 2 2"THENY$="PAIR*WINDS,SW*WAR":Y=3:RETURN
1601 B$=RIGHT$(A$,8):IFB$=" 2 2 3 3"THEN1602ELSE1610
1602 B$=LEFT$(A$,1):IFVAL(B$)<4THENY$="PAIR*WINDS":Y=1:RETURN
1610 B$=LEFT$(A$,8):IFB$=" 0 0 0 0"ORB$=" 1 1 1 1"ORB$=" 2 2 2 2"ORB$=" 3 3 3 3"ORB$=" 4 4 4 4"ORB$=" 5 5 5 5"THEN1620ELSE1630
1620 IFT=0ANDP=0THENY$="HIDDEN*QUADS":Y=2:RETURN
1621 IFT=1ORP>0THENY$="ONE*QUADS":Y=1:RETURN
1630 B$=RIGHT$(A$,8):IFB$=" 1 1 1 1"ORB$=" 2 2 2 2"ORB$=" 3 3 3 3"ORB$=" 4 4 4 4"ORB$=" 5 5 5 5"ORB$=" 6 6 6 6"THEN1620ELSE1640
1640 Y$="NO*READY*HAND":Y=-1:RETURN
2000 'ENEMY ROUTINE
2001 IFR1>0THEN2500
2010 FORI=0TO4:F(I)=E(I):NEXT:E(4)=M(4):Y=0
2011 FORI=0TO4:SWAPM(I),E(I):NEXT:T=1:GOSUB1400
2012 'Y=0 'DEBUG NO YAKU
2016 IFY>0THENBEEP:T=1:GOTO7000ELSEY=0:FORI=0TO4:SWAPM(I),E(I):E(I)=F(I):NEXT
2020 E(4)=D(TU):BEEP:TU=TU+1:GOSUB2600:LOCATE14+X,2:PRINTA$(7):GOSUB2600
2030 FORI=0TO4:SWAPM(I),E(I):NEXT:T=0:GOSUB1430
2040 'Y=0 'DEBUG NO YAKU
2055 IFY>0THENBEEP:GOTO7000ELSEFORI=0TO4:SWAPM(I),E(I):NEXT
2070 IFE=0THEN2071ELSE2090
2071 FORI=0TO4:IFE(I)>3THENEE=0:GOTO2072ELSENEXTI:GOTO2090
2072 FORJ=0TO4:IFE(J)=E(I)THENEE=EE+1
2073 NEXTJ:IFEE=1THENSWAPE(I),E(4):GOTO2100ELSENEXTI:GOTO2100
2090 IFE=1THEN2091
2091 FORI=0TO4:IFE(I)<4THENEE=0:GOTO2092ELSENEXTI:GOTO2100
2092 FORJ=0TO4:IFE(J)=E(I)THENEE=EE+1
2093 NEXTJ:IFEE=1THENSWAPE(I),E(4):GOTO2100ELSENEXTI
2100 IFI<4THENLOCATE5+X+(I*2),2:PRINT"  "+C$+"  "ELSELOCATE14+X,2:PRINT"  "+C$+"  "
2101 FORI=0TO3:SWAPM(I),E(I):NEXT:GOSUB500:FORI=0TO3:SWAPM(I),E(I):NEXT:A$=STR$(E(0))+STR$(E(1))+STR$(E(2))+STR$(E(3))
2102 RESTORE2105
2103 READD$:IFD$="*"THEN2110ELSEIFA$=D$ANDR1=0THENR1=1:BEEP:BEEP:GOTO2110
2104 GOTO 2103
2105 DATA " 0 1 2 3"," 0 0 1 2"," 0 0 2 3"," 0 1 1 2"," 0 1 1 3"," 0 1 2 2"," 0 1 3 3"," 1 2 2 3"," 1 2 3 3"," 0 2 3 3"," 4 4 5 6"," 4 5 5 6"," 4 5 6 6"," 4 4 5 5"," 4 4 6 6"," 5 5 6 6",*
2110 IFR1=1ANDEY=9THENLOCATEEX+X,11:PRINT"REACH"ELSEIFR1=1ANDEY=7THENLOCTAEEX+X,6:PRINT"REACH"
2111 LOCATEEX+X,EY:PRINTA$(E(4)):BEEP:EX=EX+2:IFEX>12THENEX=0:EY=EY-2
2112 FORI=0TO4:SWAPM(I),E(I):NEXT:GOSUB500
2113 FORI=0TO4:SWAPM(I),E(I):NEXT
2114 GOSUB2600
2120 LOCATE5+X,2:PRINT"ぃぅぃぅぃぅぃぅ   "
2130 LOCATE5+X,3:PRINT"ぇぉぇぉぇぉぇぉ   "
2135 IFTU>24THEN3500
2140 RETURN
2500 'ENEMY DISCARD WHEN REACH
2510 FORI=0TO4:F(I)=E(I):NEXT:E(4)=M(4):Y=0
2511 FORI=0TO4:SWAPM(I),E(I):NEXT:T=1:GOSUB1400
2512 'Y=0 'DEBUG NO YAKU
2516 IFY>0THENBEEP:T=1:GOTO7000ELSEY=0:FORI=0TO4:SWAPM(I),E(I):E(I)=F(I):NEXT
2520 E(4)=D(TU):BEEP:TU=TU+1:LOCATE14+X,2:PRINTA$(7):GOSUB2600
2530 FORI=0TO4:SWAPM(I),E(I):NEXT:T=0:GOSUB1430
2535 'Y=0 'DEBUG NO YAKU
2540 IFY>0THENBEEP:GOTO7000ELSEFORI=0TO4:SWAPM(I),E(I):NEXT
2550 LOCATE14+X,2:PRINT"  "+C$+"  "
2560 BEEP:LOCATEEX+X,EY:PRINTA$(E(4))
2570 EX=EX+2:IFEX>16THENEY=EY-2
2575 IFTU>24THEN3500
2580 RETURN
2600 'SHOW LEFT DRAW COUNT
2610 T=25-TU:LOCATE25+X,13:IFT>9THEN:PRINTUSING"##";T:ELSE:PRINTUSING"*#";T
2620 RETURN
3000 LOCATE4+X,9:PRINT"┌───────────┴──────┐"
3001 LOCATE4+X,10:PRINT"│*1DRAW*GAME**2RON*├"
3002 LOCATE4+X,11:PRINT"└───────────┬──────┘"
3010 A=USR(0):A$=INPUT$(1)
3020 BEEP:A=VAL(A$):IFA=1THENCLS:RETURN130ELSEIFA=0THEN3010
3030 GOTO 1500
3500 'ENEMY TURN DRAW GAME
3510 LOCATE6+X,9:PRINT"┌─────────┴─────┐"
3520 LOCATE6+X,10:PRINT"│***DRAW*GAME***├"
3530 LOCATE6+X,11:PRINT"└─────────┬─────┘"
3540 A=USR(0):A$=INPUT$(1)
3550 BEEP:CLS:GOTO 130
4000 'DORA,BLESSING(HEAVEN,EARCH,MAN) FIRST TURN, LAST TILE,REACH POINT
4010 GOSUB6000:DR$=RIGHT$(D$,LEN(D$)-1):DR=0:T$="":UR=0
4020 FORI=0TO4:IFINSTR(STR$(M(I)),DR$)>0ANDY>0THENDR=DR+1:NEXTELSENEXT
4021 IFTU=26ANDY>0THENT$="LAST*TILE"
4030 IFTU=9ANDT=1ANDY>0THENT$="BLESSING*OF*MAN"
4040 IFTU=10ANDOO=2ANDT=0ANDY>0THENT$="BLESSING*OF*EARTH"
4050 IFTU=9ANDOO=1ANDT=0ANDY>0THENT$="BLESSING*OF*HEAVEN"
4060 IFR=2ANDY>0THENR$="FIRST*TURN*WIN"ELSER$=""
4061 R1$="":IFR1>0ANDY>0THENR1$="REACH*POINT"
4070 GOSUB6100:DR$=RIGHT$(D$,LEN(D$)-1)
4080 FORI=0TO4:IFP=0ANDR>0ANDINSTR(STR$(M(I)),DR$)>0ANDY>0THENUR=UR+1:NEXTELSENEXT
5000 CLS:GOSUB5500:PRINT:PRINT"    DORA ";A$(D(26));
5001 IFR>0ANDP=0THENPRINT" URA-DORA ";A$(D(27))
5010 LOCATE5+X,3:PRINTA$(M(0))+A$(M(1))+A$(M(2))+A$(M(3))+A$(M(4))
5015 LOCATE5+X,7:PRINTY$
5020 IFR1$<>""THENLOCATE5+X,12:PRINTR1$:Y=Y+1
5021 IFDR>0ANDY>0THENLOCATE5+X,8:PRINTUSING"DORA*#";DR
5022 IFT$<>""ANDY>0THENLOCATE5+X,10:PRINTT$:Y=Y+1
5023 IFR$<>""ANDY>0THENLOCATE5+X,11:PRINTR$:Y=Y+1
5024 IFUR>0ANDY>0THENLOCATE5+X,9:PRINTUSING"URA-DORA*#";UR:DR=DR+UR
5028 LOCATE6+X,13:IFY+(DR\2)>1THENPRINTUSING"#*GRAND*SLUMS";Y+(DR\2)ELSEIFY+(DR\2)=1THENPRINT"GRAND*SLUM"
5029 IFY<0THENPRINT"GRAND*SLUM*PENALTY"
5030 A=USR(0):A$=INPUT$(1):BEEP:GOSUB5100:IFES<0ORMS<0THEN8000
5031 LOCATE3+X,17:PRINT"┌──────────────────────┐":LOCATE3+X,18:IFOY=1THENOO=1:PRINT"│*YOU*ARE*NEXT*STARTER*│"ELSEOO=2:PRINT"│*COM*IS*NEXT*STARTER**│"
5040 LOCATE3+X,19:PRINT"└──────────────────────┘":A=USR(0):A$=INPUT$(1):BEEP
5050 CLS:RETURN130
5100 'SCORE
5120 LOCATE16+X,10:PRINT"┌─────────┴┐"
5130 LOCATE16+X,11:PRINT"│**********│"
5140 LOCATE16+X,12:PRINT"└─────────┬┘"
5150 IFOO=OYTHENSC=48*(Y+(DR\2))
5160 IFOO<>OYTHENSC=32*(Y+(DR\2))
5161 IFSC>0THEN5170ELSE5210
5170 FORI=0TOSCSTEP2
5180 LOCATE16+X,11:PRINTUSING"│**###000**│";SC-I:IFI=0THENFORJ=0TO2000:NEXT
5190 BEEP:NEXT:IFOY=1THENMS=MS+SC:ES=ES-SCELSEES=ES+SC:MS=MS-SC
5200 RETURN
5210 FORI=0TOSCSTEP-2
5220 LOCATE16+X,11:PRINTUSING"│**###000**│";SC-I:IFI=0THENFORJ=0TO2000:NEXT
5230 BEEP:NEXT:IFOY=1THENMS=MS+SC:ES=ES-SCELSEES=ES+SC:MS=MS-SC
5240 RETURN
5500 'BLACK BOARD
5510 LOCATE3+X,6:PRINT"┌──────────────────────┐"
5520 LOCATE3+X,7:PRINT"│**********************│"
5530 LOCATE3+X,8:PRINT"│**********************│"
5540 LOCATE3+X,9:PRINT"│**********************│"
5550 LOCATE3+X,10:PRINT"│**********************│"
5551 LOCATE3+X,11:PRINT"│**********************│"
5552 LOCATE3+X,12:PRINT"│**********************│"
5553 LOCATE3+X,13:PRINT"│**********************│"
5560 LOCATE3+X,14:PRINT"└──────────────────────┘"
5570 RETURN
6000 'DORA
6010 IFD(26)=3THEND$=" 0":RETURN
6020 IFD(26)=6THEND$=" 4":RETURN
6030 D$=STR$((D(26)+1))
6040 RETURN
6100 'URA DORA
6110 IFD(27)=3THEND$=" 0":RETURN
6120 IFD(27)=6THEND$=" 4":RETURN
6130 D$=STR$((D(27)+1))
6140 RETURN
7000 'DORA,BLESSING(HEAVEN,EARTH,MAN),FIRST TURN,LAST TILE(ENEMY)
7010 GOSUB6000:DR$=RIGHT$(D$,LEN(D$)-1):DR=0:T$="":UR=0
7020 FORI=0TO4:IFINSTR(STR$(M(I)),DR$)>0THENDR=DR+1:NEXTELSENEXT
7021 IFTU=26THENT$="LAST*TILE"
7030 IFTU<=9ANDT=1THENT$="BLESSING*OF*MAN"
7040 IFTU=10ANDOO=1ANDT=0THENT$="BLESSING*OF*EARTH"
7050 IFTU<=9ANDOO=2ANDT=0THENT$="BLESSING*OF*HEAVEN"
7060 IFR1=2ANDY>0THENR$="FIRST*TURN*WIN"ELSER$=""
7061 R1$="":IFR>0ANDY>0THENR1$="REACH*POINT"
7070 GOSUB6100:DR$=RIGHT$(D$,LEN(D$)-1)
7080 FORI=0TO4:IFR1>0ANDINSTR(STR$(M(I)),DR$)>0THENUR=UR+1:NEXTELSENEXT
7090 CLS:GOSUB5500:PRINT:PRINT"    DORA ";A$(D(26));
7091 IFR1>0THENPRINT" URA-DORA ";A$(D(27))
7100 GOTO 5010
8000 'RESULT
8010 IFES<0THEN8500
8020 CLS
8030 LOCATE3+X,3:PRINT"┌──────────────────┐"
8040 LOCATE3+X,4:PRINT"│******************│"
8050 LOCATE3+X,5:PRINT"│*****YOU*LOSE*****│"
8060 LOCATE3+X,6:PRINT"│**-*GAME**OVER*-**│"
8070 LOCATE3+X,7:PRINTUSING"│YOUR*SCORE**###000│";MS
8080 LOCATE3+X,8:PRINT"│******************│"
8090 LOCATE3+X,9:PRINT"└──────────────────┘"
8100 LOCATE13+X,16:PRINT"┌─────────┐"
8110 LOCATE13+X,17:PRINT"│*UNLUCKY*│"
8120 LOCATE13+X,18:PRINT"└─────────┘"
8130 A=USR(0)
8140 A$=INPUT$(1):BEEP
8150 RUN40
8500 CLS
8510 LOCATE3+X,3:PRINT"┌──────────────────┐"
8520 LOCATE3+X,4:PRINT"│******************│"
8530 LOCATE3+X,5:PRINT"│*****YOU*WIN******│"
8540 LOCATE3+X,6:PRINT"│**-*GAME**OVER*-**│"
8550 LOCATE3+X,7:PRINTUSING"│YOUR*SCORE**###000│";MS
8560 LOCATE3+X,8:PRINT"│******************│"
8570 LOCATE3+X,9:PRINT"└──────────────────┘"
8580 LOCATE13+X,16:PRINT"┌───────────┐"
8590 LOCATE13+X,17:PRINT"│*HOW*MUCH?*│"
8600 LOCATE13+X,18:PRINT"└───────────┘"
8610 A=USR(0)
8620 A$=INPUT$(1):BEEP
8630 RUN40
9000 '--- CHARACTER LOADER ---
9010 SCREEN1:WIDTH32:RESTORE10000
9011 BLOAD"SUJI.BIN",S
9020 'PATTRN SET
9030 READ S$:IF S$="@" THEN GOTO 9090
9040 READ E$:S=VAL("&H"+S$):E=VAL("&H"+E$)
9050 FOR A=S*8 TO E*8+7
9060   READ D$:VPOKE A,VAL("&H"+D$)
9070 NEXT
9080 GOTO 9030
9090 'COLOR SET
9100 FOR A=&H2000 TO &H201F
9110   READ D$:VPOKE A,VAL("&H"+D$)
9120 NEXT:VPOKE&H2004,&H22
9121 VPOKE&H2005,&H71
9122 VPOKE&H2006,&H71
9123 VPOKE&H2007,&H71
9124 FORI=0TO7:VPOKE336+I,0:VPOKE360+I,0:NEXT:VPOKE363,&HFE
9130 RETURN
10000 DATA 60,90
10010 DATA 80,01,3F,01,3F,21,3F,21
10020 DATA 03,01,F9,01,F9,09,F9,09
10030 DATA 3F,03,05,09,31,00,80,FF
10040 DATA F9,81,41,21,19,01,03,FF
10050 DATA 80,01,3F,01,3F,20,24,22
10060 DATA 03,01,F9,01,F9,09,49,89
10070 DATA 27,21,27,21,21,00,80,FF
10080 DATA C9,09,C9,09,09,01,03,FF
10090 DATA 80,00,3F,04,04,04,3F,24
10100 DATA 03,01,F9,41,41,41,F9,49
10110 DATA 24,28,30,20,3F,00,80,FF
10120 DATA 49,79,09,09,F9,01,03,FF
10130 DATA 80,04,04,04,3C,04,04,04
10140 DATA 03,01,41,41,41,79,41,41
10150 DATA 04,04,06,0C,30,00,80,FF
10160 DATA 41,41,41,41,39,01,03,FF
10170 DATA 80,00,00,00,00,00,00,00
10180 DATA 03,01,01,01,01,01,01,01
10190 DATA 00,00,00,00,00,00,80,FF
10200 DATA 01,01,01,01,01,01,03,FF
10210 DATA 40,40,F0,40,40,48,30,00
10220 DATA 00,00,90,90,90,90,68,00
10230 DATA 00,00,88,88,88,50,20,00
10240 DATA 00,00,88,A8,A8,A8,50,00
10250 DATA 80,00,1E,02,04,08,17,21
10260 DATA 03,01,91,65,29,11,09,41
10270 DATA 0F,08,0E,02,1E,00,80,FF
10280 DATA 79,91,79,21,D9,01,03,FF
10290 DATA 20,20,20,00,20,20,20,00
10300 DATA C0,20,20,10,20,20,C0,00
10310 DATA 40,A8,10,00,00,00,00,00
10320 DATA 00,00,00,00,00,00,00,00
10330 DATA 80,00,01,01,01,3F,21,21
10340 DATA 03,01,01,01,01,F9,09,09
10350 DATA 21,3F,01,01,01,00,80,FF
10360 DATA 09,F9,01,01,01,01,03,FF
10370 DATA 00,78,84,84,84,84,78,00
10380 DATA 00,78,FC,FC,FC,FC,78,00
10390 DATA 40,FE,48,70,48,82,7C,00
10400 DATA 00,00,10,7E,3C,5A,34,00
10410 DATA 80,00,00,00,00,00,00,00
10420 DATA 03,01,01,01,01,01,01,01
10430 DATA 00,00,00,00,00,00,80,FF
10440 DATA 01,01,01,01,01,01,03,FF
10450 DATA 00,00,28,7C,2A,22,24,00
10460 DATA 00,00,08,5C,6A,0C,30,00
10470 DATA 00,00,08,0E,38,4C,3A,00
10480 DATA 00,00,00,3C,02,02,1C,00
10490 DATA 00,00,00,00,00,00,00,00
10500 DATA @:'COLOR
10510 DATA F0,F0,F0,F0,F0,F0,F0,F0
10520 DATA F0,F0,F0,F0,0F,0F,0F,CF
10530 DATA 6F,0A,F0,F0,F0,F0,F0,F0
10540 DATA F0,F0,F0,F0,F0,F0,F0,F0
