1 'SAVE"ｽｺﾞ6.BAS
10 'ﾎﾞｰﾄﾞｹﾞｰﾑ RPG `ｽｺﾞ6'
11 'a=F h=E p=2 x=3 ｯ=4 ｸ=5 ﾀ=6 ﾈ=9 ﾐ=A
20 SCREEN1,0:BLOAD"suji.bin",S:DEFINTA-Z:COLOR15,1,1:KEYOFF:WIDTH32:CLS:BEEP:DEFUSR=&H156
30 SPRITE$(0)="$<"+CHR$(24)+"~ｽ<$f"
40 FORI=0TO11:READA,B$:VPOKEBASE(6)+A,VAL("&H"+B$):NEXTI:GOSUB40000
50 CLS
60  PRINT"┌─┬───────┬───┬───────┬───────┐"
70  PRINT"│p│ｯ ｯ p  ﾀｯ p│p p ﾈ a│ BLUE  │"
80  PRINT"│ │ ────┬─┘ ┌─┤ ──┬─┐ │       │"
90  PRINT"│  ｯ ｯ p│p ｯﾀ○│ﾐ  ﾀｸ│ │       │"
100 PRINT"│ ┌── ┌─┴───┴─┴───┴─┘ │       │"
110 PRINT"│ │  ｯ│  ﾈ p p ﾐ p ﾈ p│       │"
120 PRINT"├ﾀ┤ ──│ ──────┐ ─ ┌── │       │"
130 PRINT"│p│ﾈ p│  p ﾐ ﾈ│  ﾈ│ﾐ ﾈ│       │"
140 PRINT"│ └─┐ └─┬─────┴───┴─┐ │       │"
150 PRINT"│ﾈ  │p  │  p ﾈ p   ﾐ│p│       │"
160 PRINT"├──ﾀ┼── │ ──────┐ ──┘ │       │"
170 PRINT"│ﾈ  │p  │ﾈ p   p│ﾈ p ﾐ├───────┤"
180 PRINT"│ ──┴───┴─────┐ └───┐ │ RED   │"
190 PRINT"│    p   p ﾈ  │ﾈ p ｯ│p│       │"
200 PRINT"├─┬─────────┐ └─┬── │ │       │"
210 PRINT"│ﾈ│ｯ ｯ ｯ ｯ p│  p│  ﾈ ｯ│       │"
220 PRINT"├ﾀ┤ ──┬─┬── ├─┐ └─┐ ──┤       │"
230 PRINT"│ｯ│ｯ ｯﾀﾈ│p pﾀﾈ│p  │p ﾈ│       │"
240 PRINT"│ └── ├─┘ ──┴─┘ ┌─┴── │       │"
250 PRINT"│p p p│  p   p p│ﾈ p  │       │"
260 PRINT"├─┬ﾀ┐ │ ──┬─┐ ──┤ ──┬─┤       │"
270 PRINT"│hﾀxﾀp│  pﾀﾈ│  p│p  ﾀ●│       │"
280 PRINT"└─┴─┴─┴───┴─┴───┴───┴─┴───────┘"
281 FORI=0TO1:X(I)=168:Y(I)=8:LV(I)=1:HP(I)=15:ST(I)=1:SP(I)=0:EX(I)=0:FD(I)=0:KY(I)=0:GL(I)=0:P(I)=0:NEXT
285 IFA$<>"2"THENP(1)=1:'2P=CPU
290 IFA$="L"ORA$="l"THENGOSUB52000
291 FORI=0TO1:GOSUB53000:NEXT
300 FORI=0TO1:GOSUB1000:NEXT:'ﾊﾟﾗﾒｰﾀﾋｮｳｼﾞ
301 I=-1
310 'ｲﾄﾞｳ
320 I=I+1:IFI>1THENI=0
330 GOSUB1500:'ﾊﾞｼｮｹｼ
340 LOCATE23,3+(I*11):PRINT"あなたのはﾞん"
350 LOCATE23,5+(I*11):PRINT"1 いとﾞう "
360 LOCATE23,6+(I*11):PRINT"2 たへﾞる "
370 LOCATE23,7+(I*11):PRINT"3 すてｰたす"
371 LOCATE23,8+(I*11):PRINT"4 せｰふﾞ"
375 LOCATE23,9+(I*11):PRINT"5 じけつ"
380 LOCATE23,10+(I*11):PRINT"1-5 ?":IFP(I)=1THENGOSUB55000ELSEGOSUB54000
385 IFA=4THENGOSUB51000:GOTO330
390 IFA=3THENGOSUB1000:GOSUB54000:SOUND13,0:GOTO330
400 IFA=2THEN700
405 IFA=5THEN24580
410 IFA<>1THENSOUND8,16:SOUND12,10:SOUND13,0:GOTO380
420 GOSUB1500
430 LOCATE23,3+(I*11):PRINT"いとﾞう"
440 LOCATE23,5+(I*11):PRINT"すきなとこてﾞ"
450 LOCATE23,6+(I*11):PRINT"とめてね":DC=USR(0):CN=0
460 DC=RND(1)*6+1:LOCATE23,8+(I*11):PRINT"-";DC;"-":SOUND8,16:SOUND12,10:SOUND13,0
465 IF P(I)=0 THEN IF INKEY$="" THEN GOTO 460
467 IF P(I)=1 THEN CN=CN+1:IF CN<20 THEN GOTO 460:'CPU WAIT
470 GOSUB1500
480 LOCATE23,3+(I*11):PRINT"いとﾞう";DC
490 LOCATE23,5+(I*11):PRINT"かｰそるきｰ"
500 LOCATE23,6+(I*11):PRINT"てﾞうこﾞか"
510 LOCATE23,7+(I*11):PRINT"して"
520 GOSUB56000 'STICK ROUTINE
530 SOUND8,16:SOUND12,10:SOUND13,0
531 X=X(I):Y=Y(I):J=0:PV=VPEEK(6144+(X(I)\8)+(Y(I)\8)*32)
540 KB=0:IF S=1 THEN Y(I)=Y(I)-8 ELSE IF S=5 THEN Y(I)=Y(I)+8
550 IF S=3 THEN X(I)=X(I)+8 ELSE IF S=7 THEN X(I)=X(I)-8
560 V=VPEEK(6144+(X(I)\8)+(Y(I)\8)*32)
570 IF V<32 THEN SOUND13,0:X(I)=X:Y(I)=Y:GOTO 520:'GRAPHIC CHR IS WALL
580 IF V=192 THEN GOSUB 650:'DOOR
581 IF V=132 THEN GOSUB 31000:FOR J=0 TO 10:SOUND13,0:NEXT:X(I)=168:Y(I)=168:GOTO 590:'WARP
582 IF V=133 THEN GOSUB 31000:FOR J=0 TO 10:SOUND13,0:NEXT:X(I)=104:Y(I)=24:GOTO 590:'WARP
583 IF V=175 AND SP(I)=0 THEN SOUND13,0:X(I)=X:Y(I)=Y:KB=1:GOTO 590:'WATER
584 IF V=184 THEN GOSUB 32000:GOTO 590:'SHOP
585 IF V=104 OR V=120 THEN 20000:'BOSS BATTLE
590 GOSUB 53000:J=J+1:IF J<2 THEN 540
600 IF KB=0 THEN DC=DC-1
610 IF DC=0 THEN 2000 ELSE 480
650 'DOOR MOVE
660 IF PV=184 OR PV=132 OR PV=133 THEN 680
670 IF KY(I)<1 THEN X(I)=X:Y(I)=Y:KB=1:GOTO 680 ELSE KY(I)=KY(I)-1
680 SOUND13,0:RETURN
700 'ﾀﾍﾞﾙ
710 GOSUB1500
720 LOCATE23,3+(I*11):PRINT"たへﾞる"
730 IFFD(I)=0THENLOCATE23,5+(I*11):PRINT"はﾟんかﾞ":LOCATE23,6+(I*11):PRINT" なｰい!":SOUND13,0:GOSUB54000:GOTO330
740 FD(I)=FD(I)-1
750 LOCATE23,5+(I*11):PRINT"はﾟくはﾟく"
760 LOCATE23,6+(I*11):PRINT"うｰん･･･｡"
770 LOCATE23,9+(I*11):PRINT"けﾞんきに"
780 LOCATE23,10+(I*11):PRINT" なった♥":GOSUB54000:HP(I)=LV(I)*5+10:GOSUB1000:GOTO320
1000 'ﾊﾟﾗﾒｰﾀﾋｮｳｼﾞ
1010 LOCATE23,3+(I*11):PRINTUSING"LEVEL##";LV(I)
1020 LOCATE23,4+(I*11):PRINTUSING"LIFE###";HP(I)
1030 LOCATE23,5+(I*11):PRINTUSING"STR ###";ST(I)
1040 LOCATE23,6+(I*11):PRINTUSING"EXP####";EX(I)
1050 LOCATE23,7+(I*11):PRINTUSING"SHIP  #";SP(I)
1060 LOCATE23,8+(I*11):PRINTUSING"KEY ###";KY(I)
1070 LOCATE23,9+(I*11):PRINTUSING"GOLD###";GL(I)
1080 LOCATE23,10+(I*11):PRINTUSING"FOOD###";FD(I)
1090 RETURN
1500 'ﾊﾞｼｮｹｼ
1510 FORJ=3+(I*11)TO10+(I*11):LOCATE23,J:PRINTSPC(7):NEXT:RETURN
2000 'ｲﾍﾞﾝﾄ ﾊﾝﾃｲ
2010 IFV=97THEN2500:'ｼﾛ
2020 IFV=200THEN3000:'ﾌｰﾄﾞ
2030 IFV=208THEN3500:'ｶﾈ
2040 IFV=112ORV=120ORV=104THEN20000:'ｾﾝﾄｳ
2100 GOSUB1000:GOTO320
2500 'ｼﾛ
2510 GOSUB1500
2520 LOCATE23,3+(I*11):PRINT" ﾈﾈﾈﾀﾀ "
2530 LOCATE23,4+(I*11):PRINT"ﾈﾈﾈﾈﾀﾀﾀ"
2540 LOCATE23,5+(I*11):PRINT" aahhh "
2550 LOCATE23,6+(I*11):PRINT" aｸｯｯh "
2560 LOCATE23,7+(I*11):PRINT" aaahh "
2570 LOCATE23,9+(I*11):PRINT"しろたﾞっ♥"
2580 LOCATE23,10+(I*11):PRINT"ここにとまろう"
2590 HP(I)=LV(I)*5+10:GOSUB54000:SOUND13,0:GOTO2100
3000 'ﾌｰﾄﾞ
3010 GOSUB1500
3020 LOCATE23,3+(I*11):PRINT"    ﾈﾈ "
3030 LOCATE23,4+(I*11):PRINT"  ﾈﾐﾐﾈﾀ"
3040 LOCATE23,5+(I*11):PRINT" ﾈﾐﾈﾈﾀﾀ"
3050 LOCATE23,6+(I*11):PRINT"ﾈﾈﾈﾀﾀﾀ "
3060 LOCATE23,7+(I*11):PRINT" ﾀﾀﾀ   "
3070 LOCATE23,9+(I*11):PRINT"はﾟんたﾞっ!"
3080 LOCATE23,10+(I*11):PRINT"やったね!"
3090 FD(I)=FD(I)+1:GOSUB54000:SOUND13,0:GOTO2100
3500 'ｶﾈ
3510 GOSUB1500
3520 LOCATE23,3+(I*11):PRINT"  aaa  "
3530 LOCATE23,4+(I*11):PRINT" aﾐﾐﾐﾈ "
3540 LOCATE23,5+(I*11):PRINT"aﾐﾐﾐﾐﾐﾈ"
3550 LOCATE23,6+(I*11):PRINT" ﾐﾐﾐﾐﾈ "
3560 LOCATE23,7+(I*11):PRINT"  ﾈﾈﾈ  "
3570 LOCATE23,9+(I*11):PRINT"おかねたﾞっ"
3580 A=RND(1)*6+1:A=A*4:LOCATE23,10+(I*11):PRINTUSING"##GOLD";A;:PRINT"!"
3590 GL(I)=GL(I)+A:GOSUB54000:SOUND13,0:GOTO2100
10000 'ｲﾛﾃﾞｰﾀ
10010 DATA6,71,7,71,12,FF,13,EE,14,22,15,33,21,44,23,55,24,66,25,99,26,AA,16,81
20000 'ｾﾝﾄｳ
20010 GOSUB1500:LOCATE23,3+(I*11):PRINT"まもののすみか":GOSUB53000
20020 A=RND(1)*(LV(I)+1):IFA>0ORV=120ORV=104THEN21000:'ｾﾝﾄｳ
20030 'ﾊｽﾞﾚ
20040 LOCATE23,5+(I*11):PRINT"するとﾞい"
20050 LOCATE23,6+(I*11):PRINT"さっきかﾞ･･"
20060 LOCATE23,8+(I*11):PRINT"しかし"
20070 LOCATE23,9+(I*11):PRINT"それはきのせい"
20080 LOCATE23,10+(I*11):PRINT"たﾞった♥":GOSUB54000:GOSUB1000:GOTO320
21000 IFV=120THEN23600ELSEIFV=104THEN23700
21001 ONAGOTO21200,22000,22500,23000,23500
21010 IFA>5THEN23500
21200 'Gｽﾗｲﾑ
21210 LOCATE23,4+(I*11):PRINT"       "
21220 LOCATE23,5+(I*11):PRINT"  aax  "
21230 LOCATE23,6+(I*11):PRINT" axxxp "
21240 LOCATE23,7+(I*11):PRINT"xxxpppp"
21250 LOCATE23,8+(I*11):PRINT" xpppp "
21260 T$="すらいむ":HP=10:ST=0:EX=1:GL=2:GOTO24000
22000 'ｵｰｸ
22010 LOCATE23,4+(I*11):PRINT"a ｸｸｯ h"
22020 LOCATE23,5+(I*11):PRINT"aｸﾐﾐﾈｯh"
22030 LOCATE23,6+(I*11):PRINT"ｸﾐ ﾐ ﾈｯ"
22040 LOCATE23,7+(I*11):PRINT"ﾐﾐﾐﾈﾈﾈﾈ"
22050 LOCATE23,8+(I*11):PRINT" ﾈaﾈhﾈ "
22060 T$="おｰく":HP=20:ST=4:EX=3:GL=5:GOTO24000
22500 'ﾌﾚｲﾑ
22510 LOCATE23,4+(I*11):PRINT"   ﾀ   "
22520 LOCATE23,5+(I*11):PRINT"  ﾀﾈﾀ  "
22530 LOCATE23,6+(I*11):PRINT" ﾀﾈﾐﾈﾀ "
22540 LOCATE23,7+(I*11):PRINT"ﾀﾈﾐﾐﾐﾈﾀ"
22550 LOCATE23,8+(I*11):PRINT" ﾀﾈﾐﾈﾀ "
22560 T$="ふれいむ":HP=30:ST=4:EX=4:GL=2:GOTO24000
23000 'ﾃﾞｰﾓﾝ
23010 LOCATE23,4+(I*11):PRINT"ｸ  a  ｯ"
23020 LOCATE23,5+(I*11):PRINT"ｸ ｸｸｯ ｯ"
23030 LOCATE23,6+(I*11):PRINT"ｸｸﾐｸﾈｯｯ"
23040 LOCATE23,7+(I*11):PRINT" ｸｸｯｯｯ "
23050 LOCATE23,8+(I*11):PRINT" ｸaｯhｯ "
23060 T$="てﾞｰもん":HP=60:ST=8:EX=7:GL=8:GOTO24000
23500 'ﾒｲｼﾞ
23510 LOCATE23,4+(I*11):PRINT"ﾀﾀ x   "
23520 LOCATE23,5+(I*11):PRINT" ﾀx p  "
23530 LOCATE23,6+(I*11):PRINT"ﾐﾀ   p "
23540 LOCATE23,7+(I*11):PRINT" ﾀxxpﾈ "
23550 LOCATE23,8+(I*11):PRINT" ﾀxppp "
23560 T$="めいしﾞ":HP=100:ST=15:EX=18:GL=28:GOTO24000
23600 'ｶﾞｲｺﾂ
23610 LOCATE23,4+(I*11):PRINT" aaaah "
23620 LOCATE23,5+(I*11):PRINT"aaｸaｯhh"
23630 LOCATE23,6+(I*11):PRINT"aaｸhｯhh"
23640 LOCATE23,7+(I*11):PRINT"aaahhhh"
23650 LOCATE23,8+(I*11):PRINT" a h h "
23660 T$="かﾞいこつ":HP=200:ST=22:EX=36:GL=35:GOTO24000
23700 'ｵｰﾎﾞｽ
23710 LOCATE23,4+(I*11):PRINT" ｯｸaｸｯ "
23720 LOCATE23,5+(I*11):PRINT"ｯｸaaaｸｯ"
23730 LOCATE23,6+(I*11):PRINT"ｯｸｸaｸｸｯ"
23740 LOCATE23,7+(I*11):PRINT"ｯｸaｸaｸｯ"
23750 LOCATE23,8+(I*11):PRINT" ｯaｸaｯ "
23760 T$="おｰほﾞす":HP=300:ST=28:EX=50:GL=50:GOTO24000
24000 'ｾﾝﾄｳ
24010 LOCATE23,9+(I*11):PRINTT$;"かﾞ"
24020 LOCATE23,10+(I*11):PRINT"あらわれた♥":GOSUB54000
24030 GOSUB30000
24031 LOCATE23,9+(I*11):PRINTUSING"おれHP###";HP(I)
24032 LOCATE23,10+(I*11):PRINTUSING"てきHP###";HP:GOSUB54000:GOSUB30000
24040 LOCATE23,9+(I*11):PRINT"1 たたかう":LOCATE23,10+(I*11):PRINT"2 にけﾞる"
24050 IF P(I)=0 THEN GOTO 24055:'HUMAN
24052 IF HP(I)<5 THEN A$="2" ELSE A$="1"
24053 GOTO 24057:'CPU
24055 A=USR(0):A$=INPUT$(1):'HUMAN
24057 SOUND13,0:A=VAL(A$):IF A>2 OR A<1 THEN 24050
24060 IF A=1 THEN 24500:'ﾀﾀｶｳ
24070 GOSUB1500:LOCATE23,3+(I*11):PRINT"すﾞさささｰっ"
24080 LOCATE23,5+(I*11):PRINT"きみはいのち"
24090 LOCATE23,6+(I*11):PRINT"からかﾞら"
24100 LOCATE23,7+(I*11):PRINT"にけﾞ"
24110 LOCATE23,8+(I*11):PRINT"  たﾞした♥":GOSUB54000:GOSUB1000:GOTO320
24500 'ﾀﾀｶｳ
24510 GOSUB30000
24520 LOCATE23,9+(I*11):PRINT"わたしは":A=RND(1)*6+1+ST(I):SOUND13,0
24530 LOCATE23,10+(I*11):PRINTUSING"###あたえた";A:HP=HP-A:GOSUB54000
24540 IF HP<1 THEN 26000:'ﾀｵｼﾀ
24550 GOSUB30000:LOCATE23,9+(I*11):PRINTT$;"に":A=RND(1)*6+1+ST:SOUND13,0
24560 LOCATE23,10+(I*11):PRINTUSING"###くらった";A:HP(I)=HP(I)-A:GOSUB54000
24570 IF HP(I)>0 THEN 24030
24580 GOSUB1500:LOCATE23,3+(I*11):PRINT"おれはしんたﾞ"
24590 LOCATE23,5+(I*11):PRINT"･･････｡"
24600 LOCATE23,7+(I*11):PRINT"･･････｡"
24610 LOCATE23,8+(I*11):PRINT"かねは"
24620 LOCATE23,9+(I*11):PRINT" はんふﾞん"
24630 LOCATE23,10+(I*11):PRINT"おしろから♥":GOSUB54000:SOUND13,0
24640 HP(I)=LV(I)*5+10:GL(I)=GL(I)\2:X(I)=168:Y(I)=8:GOSUB53000:GOSUB1000:GOTO320
26000 'ﾀｵｼﾀ
26010 GOSUB1500
26020 LOCATE23,3+(I*11):PRINTT$;"かﾞ"
26030 LOCATE23,4+(I*11):PRINT"しんてﾞくれた"
26040 LOCATE23,6+(I*11):PRINTUSING"##GOLD";GL
26050 LOCATE23,7+(I*11):PRINTUSING"##ほﾟいんと";EX
26060 LOCATE23,8+(I*11):PRINT"のけいけいち"
26070 LOCATE23,9+(I*11):PRINT"かﾞもらえた♥":SOUND13,0:GOSUB54000:SOUND13,0:IFINT(RND(1)*2)=1THENGOSUB28000:'ｶｷﾞ
26071 IFV=104THEN50000
26080 EX(I)=EX(I)+EX:GL(I)=GL(I)+GL:IFEX(I)>LV(I)*5THENEX(I)=0:GOTO27000:'ﾚﾍﾞﾈｱｯﾌﾟ
26090 GOSUB1000:GOTO320
27000 'ﾚﾍﾞﾙｱｯﾌﾟ
27010 LV(I)=LV(I)+1:GOSUB1500
27020 LOCATE23,3+(I*11):PRINT"れへﾞるかﾞ"
27030 LOCATE23,4+(I*11):PRINT"あかﾞった♥"
27040 LOCATE23,6+(I*11):PRINTUSING"HPかﾞ###";LV(I)*5+10
27050 LOCATE23,7+(I*11):PRINT"になった♥"
27060 LOCATE23,8+(I*11):PRINTUSING"STかﾞ###";ST(I)+LV(I)
27070 LOCATE23,9+(I*11):PRINT"になった♥":GOSUB54000:SOUND13,0
27080 HP(I)=HP(I)+LV(I):ST(I)=ST(I)+LV(I):GOSUB1000:GOTO320
28000 'ｶｷﾞ
28010 GOSUB1500:LOCATE23,3+(I*11):PRINT"らっきｰ♥"
28020 LOCATE23,5+(I*11):PRINT"なんとまあ!"
28030 LOCATE23,6+(I*11):PRINT"かきﾞかﾞ"
28040 LOCATE23,7+(I*11):PRINT"1こみっかった"
28050 LOCATE23,9+(I*11):PRINT"らっきｰ♥":LOCATE23,10+(I*11):PRINT"  らっきｰ♥":GOSUB54000:SOUND13,0:KY(I)=KY(I)+1:RETURN
30000 'ｹｼ
30010 FORJ=9TO10:LOCATE23,J+(I*11):PRINTSPC(7):NEXT:RETURN
31000 'ﾜｰﾌﾟ
31010 GOSUB1500:LOCATE23,3+(I*11):PRINT"わｰふﾟたﾞっ"
31020 LOCATE23,5+(I*11):PRINT"  aaa  "
31030 LOCATE23,6+(I*11):PRINT" aｸｸｸa "
31040 LOCATE23,7+(I*11):PRINT"aｸｯｯｯｸa"
31050 LOCATE23,8+(I*11):PRINT" aｸｸｸa "
31060 LOCATE23,9+(I*11):PRINT"  aaa  "
31070 GOSUB54000:GOSUB1500:RETURN
32000 'ｼｮｯﾌﾟ
32010 GOSUB1500
32020 LOCATE23,3+(I*11):PRINT"おみせのなか"
32030 LOCATE23,4+(I*11):PRINT"   aﾀﾀ "
32040 LOCATE23,5+(I*11):PRINT"   aﾀﾀ "
32050 LOCATE23,6+(I*11):PRINT"   a   "
32060 LOCATE23,7+(I*11):PRINT"ｸｸｸｸｸｯｯ"
32070 LOCATE23,8+(I*11):PRINT" ｸｯｯｯｯ "
32080 LOCATE23,9+(I*11):PRINT"おふねは 10"
32090 LOCATE23,10+(I*11):PRINT"GOLDたﾞ"
32100 GOSUB 54000:SOUND 13,0
32110 GOSUB 30000
32120 LOCATE23,9+(I*11):PRINT"かうかね?  "
32130 LOCATE23,10+(I*11):PRINT"1はい2いいえ"
32140 B=USR(0):IF P(I)=0 THEN 32145
32142 GOSUB 54000:IF SP(I)=0 AND GL(I)>9 THEN B=1:GOTO 32150 ELSE B=2:GOTO 32150:'CPU
32145 B$=INPUT$(1):B=VAL(B$):SOUND 13,0:'HUMAN
32150 IF B<1 OR B>2 THEN 32140
32160 IF B=1 THEN GL=10:GOTO 33000:'ｶｳ
32170 GOSUB1500
32180 LOCATE 23,3+(I*11):PRINT"おみせのなか"
32190 LOCATE 23,4+(I*11):PRINT"    ah "
32200 LOCATE 23,5+(I*11):PRINT"   ah  "
32210 LOCATE 23,6+(I*11):PRINT"ﾐ ah   "
32220 LOCATE 23,7+(I*11):PRINT"ﾓﾐh    "
32230 LOCATE 23,8+(I*11):PRINT"ﾀﾀﾐﾐ   "
32240 LOCATE 23,9+(I*11):PRINT"かたなは 20"
32250 LOCATE 23,10+(I*11):PRINT"GOLDたﾞ"
32260 GOSUB 54000:SOUND 13,0
32270 GOSUB 30000
32280 LOCATE 23,9+(I*11):PRINT"かうかね?  "
32290 LOCATE 23,10+(I*11):PRINT"1はい2いいえ"
32300 B=USR(0):IF P(I)=0 THEN 32305
32302 GOSUB 54000:IF GL(I)>19 AND GL(I)<50 THEN B=1:GOTO 32310 ELSE B=2:GOTO 32310:'CPU
32305 B$=INPUT$(1):B=VAL(B$):SOUND13,0:'HUMAN
32310 IF B<1 OR B>2 THEN 32300
32320 IF B=1 THEN GL=20:GOTO 33000:'ｶｳ
32330 GOSUB 1500
32340 LOCATE 23,3+(I*11):PRINT"おみせのなか"
32350 LOCATE 23,4+(I*11):PRINT"    ah "
32360 LOCATE 23,5+(I*11):PRINT"   ah  "
32370 LOCATE 23,6+(I*11):PRINT"  ﾈﾀ   "
32380 LOCATE 23,7+(I*11):PRINT" ﾈﾀ    "
32390 LOCATE 23,8+(I*11):PRINT"ﾈﾀ     "
32400 LOCATE 23,9+(I*11):PRINT"やりは 50"
32410 LOCATE 23,10+(I*11):PRINT"GOLDたﾞ"
32420 GOSUB 54000:SOUND 13,0
32430 GOSUB 30000
32440 LOCATE 23,9+(I*11):PRINT"かうかね?  "
32450 LOCATE 23,10+(I*11):PRINT"1はい2いいえ"
32460 B=USR(0):IF P(I)=0 THEN 32465
32462 GOSUB 54000:IF GL(I)>49 THEN B=1:GOTO 32470 ELSE B=2:GOTO 32470:'CPU
32465 B$=INPUT$(1):B=VAL(B$):SOUND 13,0:'HUMAN
32470 IF B<1 OR B>2 THEN 32460
32480 IF B=1 THEN GL=50:GOTO 33000:'ｶｳ
32490 GOSUB 1500
32500 LOCATE 23,3+(I*11):PRINT"おみせから"
32510 LOCATE 23,4+(I*11):PRINT" てﾞますか?"
32520 LOCATE 23,6+(I*11):PRINT"1はい2いいえ"
32530 B=USR(0):IF P(I)=0 THEN 32535
32532 GOSUB 54000:B=1:GOTO 32540:'CPU
32535 B$=INPUT$(1):B=VAL(B$):SOUND 13,0:'HUMAN
32540 IF B<1 OR B>2 THEN 32530
32550 IF B=1 THEN GOSUB 1500:RETURN ELSE 32000
33000 'ｶｳ
33010 GOSUB 1500
33020 LOCATE 23,3+(I*11):PRINT"おれ の かね"
33030 LOCATE 23,4+(I*11):PRINTUSING"###GOLD";GL(I)
33040 LOCATE 23,5+(I*11):PRINT"ねたﾞん"
33050 LOCATE 23,6+(I*11):PRINTUSING"###GOLD";GL
33060 LOCATE 23,8+(I*11):PRINT"さしひき･･｡"
33070 LOCATE 23,10+(I*11):PRINT"･･････｡"
33080 GOSUB 54000:SOUND 13,0
33090 GOSUB 1500
33100 IF GL(I)-GL>=0 THEN 34000
33110 LOCATE 23,3+(I*11):PRINT"ちょっと"
33120 LOCATE 23,4+(I*11):PRINT"   ちょっと"
33130 LOCATE 23,6+(I*11):PRINT"おきゃくさん"
33140 LOCATE 23,8+(I*11):PRINT"かねかﾞ"
33150 LOCATE 23,9+(I*11):PRINT"たりませんせﾞ"
33160 GOSUB 54000:SOUND 13,0:GOTO 32000
34000 LOCATE 23,3+(I*11):PRINT"おつりは"
34010 LOCATE 23,4+(I*11):PRINTUSING"###GOLD";GL(I)-GL
34020 LOCATE 23,5+(I*11):PRINT"てﾞす｡"
34030 LOCATE 23,7+(I*11):PRINT"まいとﾞありｰ"
34040 LOCATE 23,8+(I*11):PRINT"またきて"
34050 LOCATE 23,9+(I*11):PRINT" おくれやす♥"
34060 GOSUB 54000:SOUND 13,0
34070 GL(I)=GL(I)-GL
34080 IF GL=10 THEN SP(I)=SP(I)+1
34090 IF GL=20 THEN ST(I)=ST(I)+3
34100 IF GL=50 THEN ST(I)=ST(I)+10
34110 GOSUB 1500:RETURN
40000 'ﾀｲﾄﾙ
40010 CLS:LOCATE12,0:PRINT"=♥RPG♥="
40020 PRINT"      ﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈaﾈaﾈﾈﾈﾈ"
40030 PRINT"     ﾈaaaaaxxaaaaahxhxxxﾈﾈ"
40040 PRINT"     ﾈhhhhaxxhhhha x xxxxﾈ"
40050 PRINT"     ﾈ    axx    apxｯｯｯｯｯﾈ"
40060 PRINT"     ﾈpppahxxppppaxxｯ    ﾈ"
40070 PRINT"ﾀﾈﾀﾈﾀﾈxxah xxxxxxaxxｯｯｯｯｯﾈﾀﾈﾀﾈﾀﾈ";
40080 PRINT"ﾀﾈﾀﾈﾀﾈxahapxxxxxxaxxｯ   ｯﾈﾀﾈﾀﾈﾀﾈ";
40090 PRINT"     ﾈah haxxaaaaaxxｯｯｯｯｯﾈ"
40100 PRINT"     ﾈh p hxxhhhhhxx     ﾈ"
40110 PRINT"     ﾈ pxp xx     xxppppﾈﾈ"
40120 PRINT"      ﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈﾈ"
40130 FORI=0TO1:PUTSPRITEI,(0,0),0:NEXT
40135 LOCATE9,16:PRINT"PUSH ANY KEY"
40140 LOCATE10,17:PRINT"(2:2P MODE)"
40141 LOCATE11,18:PRINT"(L:LOAD)"
40150 LOCATE0,22:PRINT"1989,1991,2023 (ひ) HIGCHI'N SOFT";
40160 A=USR(0):A$=INPUT$(1):SOUND8,16:SOUND12,10:SOUND13,0
40170 RETURN
50000 'ｴﾝﾃﾞｨﾝｸﾞ
50010 GOSUB1500
50020 LOCATE23,3+(I*11):PRINT"｢くﾞはっ｣"
50030 LOCATE23,5+(I*11):PRINT"｢よくも･･｣"
50040 LOCATE23,7+(I*11):PRINT"｢くちおしや｣"
50050 LOCATE23,9+(I*11):PRINT"｢･････｣"
50060 A=USR(0):A$=INPUT$(1):SOUND13,0
50070 GOSUB1500
50080 LOCATE23,3+(I*11):PRINT"そのこﾞ･･･"
50090 LOCATE23,4+(I*11):PRINT"   ﾐ   "
50100 LOCATE23,5+(I*11):PRINT"ﾐ ﾐﾀﾈ ﾈ"
50110 LOCATE23,6+(I*11):PRINT"ﾐﾐﾐﾐﾈﾈﾈ"
50120 LOCATE23,7+(I*11):PRINT" h h h "
50130 LOCATE23,8+(I*11):PRINT" hhhhh "
50140 LOCATE23,9+(I*11):PRINT"きみは"
50150 LOCATE23,10+(I*11):PRINT"いちはやく"
50160 A=USR(0):A$=INPUT$(1):SOUND13,0
50170 GOSUB30000
50180 LOCATE23,9+(I*11):PRINT"へいわにした"
50190 LOCATE23,10+(I*11):PRINT"らっきｰ♥"
50200 A=USR(0):A$=INPUT$(1):SOUND13,0
50210 GOSUB1500
50220 LOCATE23,3+(I*11):PRINT" お わ り"
50230 LOCATE23,4+(I*11):PRINT"  ﾈﾈﾀ  "
50240 LOCATE23,5+(I*11):PRINT" ﾐﾐﾐﾈﾈ "
50250 LOCATE23,6+(I*11):PRINT"  aah  "
50260 LOCATE23,7+(I*11):PRINT"  aah  "
50270 LOCATE23,8+(I*11):PRINT"  aah  "
50280 LOCATE23,9+(I*11):PRINT"  aah  "
50290 LOCATE23,10+(I*11):PRINT"ﾀﾀﾀﾀﾀﾀﾀ"
50300 GOTO 50300
51000 'ｾｰﾌﾞ
51001 SOUND13,0:GOSUB1500
51002 LOCATE23,3+(I*11):PRINT"せｰふﾞします"
51003 LOCATE23,5+(I*11):PRINT"ちょっと"
51004 LOCATE23,6+(I*11):PRINT"  まってね♥"
51010 OPEN"ｽｺﾞ6.DAT"FOROUTPUTAS#1
51020 FORII=0TO1
51030 PRINT#1,X(II)
51040 PRINT#1,Y(II)
51050 PRINT#1,LV(II)
51060 PRINT#1,HP(II)
51070 PRINT#1,ST(II)
51080 PRINT#1,SP(II)
51090 PRINT#1,EX(II)
51100 PRINT#1,FD(II)
51110 PRINT#1,KY(II)
51120 PRINT#1,GL(II)
51125 PRINT#1,P(II)
51130 NEXT
51140 CLOSE(1):LOCATE23,9+(I*11):PRINT"おっけｰ♥"
51150 RETURN
52000 'ﾛｰﾄﾞ
52002 LOCATE23,3:PRINT"ろｰとﾞします"
52003 LOCATE23,5:PRINT"ちょっと"
52004 LOCATE23,6:PRINT"  まってね♥"
52010 OPEN"ｽｺﾞ6.DAT"FORINPUTAS#1
52020 FORII=0TO1
52030 INPUT#1,X(II)
52040 INPUT#1,Y(II)
52050 INPUT#1,LV(II)
52060 INPUT#1,HP(II)
52070 INPUT#1,ST(II)
52080 INPUT#1,SP(II)
52090 INPUT#1,EX(II)
52100 INPUT#1,FD(II)
52110 INPUT#1,KY(II)
52120 INPUT#1,GL(II)
52125 INPUT#1,P(II)
52130 NEXT
52140 CLOSE(1):LOCATE23,9:PRINT"おっけｰ♥"
52150 RETURN
53000 'SPRITE UPATE
53005 FOR II=0 TO 1
53010 O=0:IF X(0)=X(1) AND Y(0)=Y(1) THEN O=-1+II*2
53020 PUTSPRITEII,(X(II)+O,Y(II)-1),7+II,0:NEXT
53030 RETURN
54000 'INPUT KEY
54010 A=USR(0)
54015 IF P(I)=0 THEN A$=INPUT$(1):A=VAL(A$):RETURN:'HUMAN
54020 FOR W=0 TO 20:'CPU WAIT
54030 IF INKEY$<>"" THEN:A=RND(1)*R+1:RETURN
54040 NEXT:RETURN
55000 'CPU COMMAND ROUTINE
55010 IF HP(I)<5+LV(I) AND FD(I)>0 THEN A=2:RETURN:'FOOD
55020 A=1:RETURN:'MOVE
56000 'INPUT MOVE DIRECTION
56010 IF P(I)=0 THEN S=STICK(0):IF S=0 THEN 56010 ELSE RETURN:'HUMAN
56020 S=RND(1)*4:S=S*2+1
56022 X=X(I):Y=Y(I):'CPU
56025 IF S=1 THEN Y=Y-8 ELSE IF S=5 THEN Y=Y+8
56027 IF S=3 THEN X=X+8 ELSE IF S=7 THEN X=X-8
56030 V=VPEEK(6144+(X\8)+(Y\8)*32)
56040 IF V>31 THEN RETURN
56050 S=S+2:IF S>7 THEN S=1
56060 GOTO56022